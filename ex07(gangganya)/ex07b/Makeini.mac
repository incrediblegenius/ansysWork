/prep7
nint=arg1							!!积分点数量
Af=tf*b						  	!!单个翼缘面积
Aw=(h-2*tf)*tw		  		!!腹板面积
insf=arg2								!!翼缘残余应力峰值和fy的比值(负号为压应力)
insw=-insf*Af/(Af+Aw)		!!截面上要自平衡

*dim,srint,,nint	    !!积分点处残余应力sr
*dim,yint,,nint		    !!积分点y坐标
*dim,zint,,nint		    !!积分点z坐标

!!!!!!!!!!读入积分点坐标!!!!!!!!!
*vread,yint(1),intcord,txt,,,nint,,,,
(21x,e15.5,15x)
*vread,zint(1),intcord,txt,,,nint,,,,
(36x,e15.5)

!!!!!!!!!!调试程序，用于输出积分点坐标!!!!!!!!!
!/out,intcord1,txt
!*vwrite,yint(1),zint(1)
!(21x,2e15.5)
!/out
!!!!!!!!!!调试完毕!!!!!!!!!

!!!计算各积分点处的残余应力值
*do,ii,1,nint
 *if,abs(yint(ii)),le,tw/2,then   !!!腹板上的残余应力
  srint(ii)=insw*Fy   
 *else           !!!!翼缘残余应力
  srint(ii)=(insw+(insf-insw)/(b/2)*abs(yint(ii)))*Fy
 *endif
*enddo

!!!计算各积分点处的残余应力值
inistate,set,csys,-2				!!按单元坐标系输入残余应力
inistate,set,dtyp,stre			!!输入应力值
*do,ii,1,nint/4
 *do,jj,1,4
  ii1=4*(ii-1)+jj
  inistate,define,,,ii,jj,srint(ii1),0,0,0,0,0
 *enddo
*enddo
