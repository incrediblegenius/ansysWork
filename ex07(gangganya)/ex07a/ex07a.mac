!!!!第07讲：钢压杆的极限承载力
!!!!算例Ex07a
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
finish
/clear
/filname,ex07a

!!!基本参数输入
/prep7
PI=3.1415926535
h=0.10              !!!截面高度
b=0.05                 !!!截面宽度
tw=0.004           !!!腹板厚度
tf=0.005           !!!翼缘厚度
L=arg1                   !!!计算长度
v0=L/arg2                !!!初始弯曲  
Fy=235.0e6            !!!屈服强度
Ex=2.06e11            !!!弹性模量
Nu=0.3                !!!柏松比
Area=(b*h-(b-tw)*(h-2*tf))    !!!截面面积
Ny=Area*Fy                     !!!全截面屈服荷载A*fy
NN=10                       !!!分段总数
dL=L/NN

!!!定义单元类型、材料特性
/prep7
et,1,beam188
keyopt,1,3,2        !!高阶形函数
mp,ex,1,Ex
mp,prxy,1,Nu
tb,bkin,1,1,2,1     !!定义非线性
tbdata,,Fy,0
sectype,1,beam,I,,2
secoffset,cent
secdata,b,b,h,tf,tf,tw

!!!定义单元和节点
*do,i,1,NN+1
 y=dL*(i-1)
 x=sin(y/L*PI)*v0      !!输入的初始缺陷
 n,i,x,y,0
*enddo
n,NN+2,0,0,1e10
*do,i,1,NN
 e,i,i+1,NN+2
*enddo

!!!定义边界条件,施加荷载
d,1,ux,0
d,1,uy,0
d,1,uz,0
d,1,roty,0
d,NN+1,ux,0
d,NN+1,uz,0
d,NN+1,roty,0
f,NN+1,fy,-Ny

!!!求解
/solu
time,1
! eresx,on    !!积分点外推
nlgeom,on
nsubst,100
outres,all,all
arclen,on       !!打开弧长法
arctrm,u,0.05*L,NN/2+1,ux       !!弧长法截断条件
solve

! !!!后处理，画荷载位移曲线
! /post26
! nsol,2,NN/2+1,u,x,ux_2 
! xvar,2
! plvar,1

/post1          !!批量输出
*get,nset,active,0,set,nset
*do,iii,1,nset-1        !!找到极值的循环
 set, , ,1, , , ,iii,
 *get,fi0,active,0,set,time
 *if,fi0,ge,fimax,then
   fimax=fi0
 *endif
*enddo
/out,fiH,txt,,append    !!输出到fiH.txt文件（以append模式输出
*vwrite,L,fimax
(2f20.10)
/out



