!!Class 2 钢筋混凝土网壳

finish
/clear
/filname,ex02c

!!! input parameter
/prep7
PI=3.1415926535
R=15    !!球面半径
f=5     !!网壳矢高
Nr=30    !!径向划分数量
Nc=90   !!环向划分数量
alf=acos((R-f)/R)*180/PI        !!圆心角(ard)
alf0=90-alf                     !!起始圆心角
dalf=alf/Nr                     !!每个网格对应圆心角(ard)
dbeta=360/Nc
Ex=3.0e10                      !!弹性模量
Nu=0.2                          !!泊松比
! b=0.0811                        !!方管截面宽度
! h=0.15                          !!方管截面高度
t=0.08                         !!方管壁厚（不用板了，只用厚度）
Fz=1000                        !!节点载荷

!!前处理
/prep7
et,1,shell181                !!define units' type（高级板壳）
! keyopt,1,3,2                !!开启高阶形函数（重要）
mp,ex,1,Ex                  !!define the character of material
mp,nuxy,1,Nu
! sectype,1,beam,hrec,,1      !!定义截面类型（这里不用截面）
! secoffset,cent              !!截面按形心对中
! secdata,b,h,t,t,t,t         !!定义截面参数
r,1,t

!!定义节点
Nnode=Nc*Nr+1           !!节点总数
N,Nnode,0,0,R           !!顶点0，0，R
csys,2                  !!球面坐标系
*do,i,1,Nr
  *do,j,1,Nc
    inode=Nc*(i-1)+j        !!当前节点号
    n,inode,R,(j-1)*dbeta,alf0+(i-1)*dalf   !!定义inode的球面坐标
  *enddo
*enddo
csys,0                      !!直角坐标系

!!定义单元
type,1
secnum,1
mat,1
*do,i,1,Nr-1
  *do,j,1,Nc
    inode=Nc*(i-1)+j            !!每环首节点号
    *if,j,ne,Nc,then
      e,inode,inode+1,inode+1+Nc,inode+Nc       !!成面！！！
    *else
      e,inode,inode-Nc+1,inode+1,inode+Nc
    *endif
  *enddo
*enddo
*do,i,Nr,Nr
  *do,j,1,Nc
    inode=Nc*(i-1)+j            !!每环首节点号
    *if,j,ne,Nc,then
      e,inode,inode+1,Nnode
    *else
      e,inode,inode-Nc+1,Nnode
    *endif
  *enddo
*enddo

!!施加约束
nsel,s,,,1,Nc
d,all,ux,0
d,all,uy,0
d,all,uz,0

!!施加荷载
nsel,s,,,Nc+1,Nnode
f,all,fz,-Fz
nsel,all

!!进行静力分析
/solu
antype,0
pstres,on       !!打开预应力开关
solve
finish          !!结束求解

!!屈曲分析
/solu
antype,1
bucopt,lanb,10
mxpand,10,,,1
solve

!!后处理
/post1
set,1,1
plnsol,u,z,0,1.0

!!显示设置
/view,1,1,1,1
/ang,1
/ang,1,-120,zs,1
/rep,fast


    