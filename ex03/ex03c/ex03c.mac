!!Beam188(需要计算弯矩)+弯弹簧
!!认识应力与刚度

finish
/clear
/filname,ex03c

!!input parameters
/prep7
Ex=2.0e11
b=0.3   !!宽度
L=1
k=1000
p=1000
NN=4    !!分段数量

!定义单元类型，常数，材料类型
et,1,beam188
mp,ex,1,Ex
sectype,1,beam,rect     !!定义截面类型
secoffset,cent          !!形心对中
secdata,b,b             !!截面参数
et,2,combin14           !!弹簧
keyopt,2,2,6            !!定义扭转弹簧（绕z轴的扭转弹簧）
r,2,k

!!定义节点
*DO,i,1,NN+1
    n,2*i-1,0,(i-1)*L
    n,2*i,0,i*L
*ENDDO

!!定义梁单元
type,1
secnum,1
mat,1
*DO,i,1,NN+1
    e,2*i-1,2*i
*ENDDO

!!定义弹簧单元
type,2
real,2
mat,2
*DO,i,1,NN
    e,2*i,2*i+1
*ENDDO

!!施加约束
d,all,uz,0
d,all,rotx,0
d,all,roty,0
d,1,ux,0
d,1,uy,0
d,2*NN+2,ux,0
cpintf,ux,0         !!让所有节点重合的点在x方向平动连接
cpintf,uy,0         !!同理在y方向

!!载荷
f,2*NN+2,fy,-p

!!静力分析
/solu
antype,0
pstres,on
solve
finish

!!屈曲分析
/solu
antype,1
bucopt,subs,NN
mxpand,NN,,,1
solve

!!后处理
/post1
set,1,1
esel,s,type,,1          !!选择梁单元
plnsol,u,x,0,1.0

!!读取刚度矩阵
!getK,'ex03c',1